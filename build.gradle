plugins {
    id 'com.github.ben-manes.versions' version '0.17.0'
}

allprojects {

    // jgnash-help is not a compiled sub-project
    if (!it.name.startsWith('jgnash-help')) {
        apply plugin: 'java'
    }

    apply plugin: 'findbugs'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    group = 'jgnash'
    version = '2.36.0'

    project.ext {
        junitVersion = '5.2.0'
        junitextensionsVersion = '2.2.0'

        picocliVersion = '3.5.2'

        apachePoiVersion = '4.0.0'
        commons_cvs = '1.5'
        commons_text = '1.1'

        h2Version = '1.4.197'
        hsqldbVersion = '2.4.1'

        xstreamVersion = '1.4.10'

        hibernateVersion = '5.3.5.Final'
        hikariVersion = '3.2.0'

        dynamicJasperVersion = '5.1.1'
        jasperReportsVersion = '6.4.3'
        itextVersion = '2.1.7.js6'

        openHtmlToPDFVersion = '0.0.1-RC15'

        nettyVersion = '4.1.29.Final'

        slf4jVersion = '1.8.0-beta2'

        jfreeChartVersion = '1.0.19'
        substanceVersion = '7.3'
        swingxVersion = '1.6.5-1'

        awaitilityVersion = '3.1.2'
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    findbugs {
        ignoreFailures = true
        excludeFilter = file("$rootProject.projectDir/findbugs-exclude-filter.xml")
    }
}

subprojects {
    repositories {
        mavenCentral()
        jcenter()
        mavenLocal()
        // maven { url "http://nexus.fdvs.com.ar/content/groups/public/" }
        maven { url "http://jaspersoft.jfrog.io/jaspersoft/third-party-ce-artifacts/" }
    }

    // jgnash-help is not a compiled sub-project
    if (!it.name.startsWith('jgnash-help')) {
        dependencies {

            testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitVersion
            testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: junitVersion
            testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion

            testCompile group: 'io.github.glytching', name: 'junit-extensions', version: junitextensionsVersion
            testCompile group: 'org.awaitility', name: 'awaitility', version: awaitilityVersion
            testCompile group: 'org.apache.commons', name: 'commons-text', version: commons_text
        }
    }

    test {
        useJUnitPlatform()

        //we want display the following test events
        testLogging {
            events "PASSED", "STARTED", "FAILED", "SKIPPED"
            showStandardStreams = true
        }
    }
}

wrapper {
    gradleVersion = '4.10.1'
}

task distZip {
    doLast {
        ant.zip(destfile: 'jgnash-' + version + '-bin.zip', duplicate: 'preserve', update: 'false') {
            zipgroupfileset(dir: '.', includes: 'jgnash-swing/build/distributions/jGnashSwing-' + version + '.zip')
            zipgroupfileset(dir: '.', includes: 'jgnash-fx/build/distributions/jGnashFx-' + version + '.zip')
        }
    }
}

distZip.dependsOn(":jgnash-fx:distZip")
distZip.dependsOn(":jgnash-swing:distZip")
